buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

if (new File("curseKey.properties").exists()) {
    // define the properties file
    ext.configFile = file "curseKey.properties"

    configFile.withReader {
        // read config.  it shall from now on be referenced as simply config or as project.config
        def prop = new Properties()
        prop.load(it)
        project.ext.curse = new ConfigSlurper().parse prop
    }
}

repositories {
    maven {
        name = "IGW"
        url = "http://maven.k-4u.nl/"
    }
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { // the repo from which to get NEI and stuff
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven { // the repo from which to get EnderIO and tterags mods
        name 'tterrag'
        url "http://maven.tterrag.com/"
    }
    maven {
        name = "OC repo"
        url = "http://maven.cil.li/"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    ivy {
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
    }
    ivy {
        name "CoFHCore"
        artifactPattern "http://addons-origin.cursecdn.com/files/2277/479/[module]-[revision]-[classifier].[ext]"
    }
    ivy {
        name "AgriCraft"
        artifactPattern "http://addons-origin.cursecdn.com/files/2271/838/[module]-[revision]-[classifier].[ext]"
    }
}
apply plugin: 'forge'
sourceCompatibility = 1.7
targetCompatibility = 1.7

// define the properties file
ext.configFile = file "project.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}"
if (hasProperty('appendBuildNumber')) {
    version = version + "-" + appendBuildNumber.toString()
} else {
    version = version + "-0"
}

group = "${config.group_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${config.mod_id}-${config.minecraft_version}"

minecraft {
    version = "${config.forge_version}"
    if (file('../run').exists()) {
        runDir = "../run"
    } else {
        runDir = "assets"
    }

    replace '@VERSION@', project.version
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

build.dependsOn sourceJar, deobfJar

dependencies {
    // I dont have to specify NEI.. because gradle magic. aka: transitive dependency resolution
    compile "igwmod:IGW-Mod-${config.minecraft_version}:${config.igwmod_version}:userdev"
    compile "mcp.mobius.waila:Waila:${config.waila_version}"
    compile "codechicken:CodeChickenLib:${config.minecraft_version}-${config.ccl_version}:dev"
    compile "codechicken:CodeChickenCore:${config.minecraft_version}-${config.ccc_version}:dev"
    compile "net.industrial-craft:industrialcraft-2:${config.ic2_version}:dev"
    compile name: "buildcraft", version: "${config.bc_version}", classifier: "dev"
    compile "com.enderio:EnderIO:${config.minecraft_version}-${config.enderio_version}:dev"
    compile name: "CoFHCore", version: "[${config.minecraft_version}]${config.cofhcore_version}", classifier: "dev", ext: "jar"
    compile name: "AgriCraft", version: "${config.minecraft_version}-${config.agricraft_version}", classifier: "api", ext: "jar"
}

// this fixes dev environments for IDEA 14 users.
sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}
